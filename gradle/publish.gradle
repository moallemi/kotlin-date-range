
project.description = "Implementing rangeTo operator for LocalDate and Date in kotlin"
project.group = "me.moallemi.tools"
project.version = "0.0.3"
project.ext {
    artifactId = 'kotlin-date-range'
    name = 'Kotlin Date Range'
    url = "https://github.com/moallemi/kotlin-date-range"
    scm = [
            connectionUrl         : "scm:git@github.com:moallemi/kotlin-date-range.git",
            developerConnectionUrl: "scm:git@github.com:moallemi/kotlin-date-range.git"
    ]
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set("sources")
}

task javadocJar(type: Jar) {
    from tasks.javadoc
    archiveClassifier.set("javadoc")
}

publishing {
    publications {
        kotlinDateRange(MavenPublication) {
            groupId = project.group
            artifactId = project.artifactId
            version = project.version

            from components.java
            artifact tasks["sourcesJar"]
            artifact tasks["javadocJar"]

            pom {
                name = project.name
                packaging "jar"
                description = project.description
                url = project.url
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'moallemi'
                        name = 'Reza Moallemi'
                    }
                }
                scm {
                    url = project.url
                    connection = project.scm.connectionUrl
                    developerConnection = project.scm.developerConnectionUrl
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username System.getenv('SONATYPE_USERNAME')
                password System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}

ext."signing.keyId" = System.getenv('SIGNING_KEYID')
ext."signing.password" = System.getenv('SIGNING_PASSWORD')
ext."signing.secretKeyRingFile" = System.getenv('SIGNING_SECRET_KEYRING_FILE')

signing {
    required !version.endsWith('SNAPSHOT')
    sign(publishing.publications["kotlinDateRange"])
}

bintray {
    user = System.getenv('BINTRAY_USERNAME')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['kotlinDateRange']
    dryRun = false
    pkg {
        repo = 'maven'
        name = 'kotlin-date-range'
        desc = project.description
        websiteUrl = project.url
        issueTrackerUrl = 'https://github.com/moallemi/kotlin-date-range/issues'
        vcsUrl = 'https://github.com/moallemi/kotlin-date-range.git'
        licenses = ['Apache-2.0']
        labels = ['kotlin', 'android', 'date', 'local-date', 'date-range']
        publicDownloadNumbers = false
        version {
            name = project.version
            //desc = 'optional, version-specific description'
            //released  = 'optional, date of the version release' //2 possible values: date in the format of 'yyyy-MM-dd'T'HH:mm:ss.SSSZZ' OR a java.util.Date instance
            //vcsTag = '1.3.0'
            // attributes = [] //Optional version-level attributes
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = System.getenv('BINTRAY_SIGNING_PASSWORD') //Optional. The passphrase for GPG signing'
            }
            mavenCentralSync {
                sync = false //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = System.getenv('SONATYPE_USERNAME') //OSS user token
                password = System.getenv('SONATYPE_PASSWORD') //OSS user password
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }

}